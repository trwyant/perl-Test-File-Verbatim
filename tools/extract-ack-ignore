#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use ExtUtils::MakeMaker;
use Getopt::Long 2.33 qw{ :config auto_version };
use List::Util 1.45 qw{ uniqstr };
use Module::Load::Conditional qw{ check_install };
use Pod::Usage;

our $VERSION = '0.000_007';

use constant CONFIG_DEFAULT	=> 'App::Ack::ConfigDefault';

use constant REF_ARRAY		=> ref [];

my %opt;

GetOptions( \%opt,
    help => sub { pod2usage( { -verbose => 2 } ) },
) or pod2usage( { -verbose => 0 } );

my $config = check_install( module => CONFIG_DEFAULT )
    or die "Module @{[ CONFIG_DEFAULT ]} not installed\n";

my %ignore;

{
    local $_ = undef;	# while (<>) does not localize $_.

    open my $fh, '<', $config->{file}
	or die "Unable to open $config->{file}: $!\n";

    while ( <$fh> ) {
	chomp;
	my ( $what, $test, $data ) = m/(?|
	    --ignore-(?|(file)|(dir)ectory)=([^:]+):(.*) |
	    --ignore-(dir)(=)(.*)
	)/smx
	    or next;
	$test eq '='
	    and $test = 'is';
	my $code = __PACKAGE__->can( "record_$test" )
	    or die "Test '$test' not recognized in '$_'\n";
	$code->( $what => $data );
    }
    close $fh;
}

foreach my $key ( keys %ignore ) {
    ref $ignore{$key}{match} eq REF_ARRAY
	or next;
    my @re = uniqstr( sort @{ $ignore{$key}{match} } );
    local $" = '|';
    $ignore{$key}{match} = qr<@re>;
}

my $ack_version = do {
    my $rv = check_install( module => 'App::Ack' );
    MM->parse_version( $rv->{file} );
};

my $ignore_str = do {
    use Data::Dumper;
    my $dd = Data::Dumper->new( [ \%ignore ], [ qw{ $ignore } ] );
    $dd->Indent( 1 );
    $dd->Pad( '    ' );
    $dd->Sortkeys( 1 );
    $dd->Trailingcomma( 1 );
    $dd->Dump();

=begin comment

    use Data::Dump;
    sprintf '$ignore = %s;', Data::Dump::dump( \%ignore );

=end comment

=cut

};

$ignore_str =~ s/ \A \s+ //smx;

print <<"EOD";

# Generated by $0 from @{[ CONFIG_DEFAULT ]},
# which is part of ack $ack_version
sub exclude_ack_ignore {
    use File::Spec;
    state $ignore_str
EOD

print <<'EOD';
    my $what = -d _ ? 'dir' : 'file';
    local $_ = ( File::Spec->splitpath( $_ ) )[2];
    $ignore->{$what}{is}{$_}
	and return 1;
    if ( ( my $dot = rindex $_, '.' ) >= 0 ) {
	my $ext = substr $_, $dot + 1;
	$ignore->{$what}{ext}{$ext}
	    and return 1;
    }
    $ignore->{$what}{match}
	or return 0;
    return $_ =~ $ignore->{$what}{match};
}
EOD

sub record_ext {
    my ( $what, $data ) = @_;
    foreach ( split /,/, $data ) {
	$ignore{$what}{ext}{$_} = 1;
    }
    return;
}

sub record_is {
    my ( $what, $data ) = @_;
    $ignore{$what}{is}{$data} = 1;
    return;
}

sub record_match {
    my ( $what, $data ) = @_;
    $data =~ s/ ( . ) ( .* ) \1 \z /$2/smx;
    push @{ $ignore{$what}{match} }, $data;
    return;
}

__END__

=head1 TITLE

extract-ack-ignore - Generate exclude_ack_ignore() from App::Ack::ConfigDefault

=head1 SYNOPSIS

 extract-ack-ignore
 extract-ack-ignore --help
 extract-ack-ignore --version

=head1 OPTIONS

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script reads
L<App::Ack::ConfigDefault|App::Ack::ConfigDefault>, finds all the
C<--ignore-file> and C<--ignore-dir> specifications, and writes the
source of Perl subroutine C<exclude_ack_ignore()> to STDOUT.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2022 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
